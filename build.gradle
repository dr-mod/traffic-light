plugins {
    id 'org.hidetake.ssh' version '2.6.0'
    id 'java'
}

remotes {
    raspberry {
        host = '192.168.170.254'
        user = 'pi'
        identity = file('id_rsa')
    }
}

jar {
    manifest {
        attributes "Main-Class": "org.drmod.EntryPoint"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task dist(dependsOn: [build]) << {
    ssh.run {
        session(remotes.raspberry) {
            put from: 'build/libs/traffic-light.jar', into: '/home/pi/'
            executeSudo 'java -jar /home/pi/traffic-light.jar', pty: true
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile('com.pi4j:pi4j-core:1.1')
    compile('org.springframework:spring-context:4.1.1.RELEASE')
    compile('org.slf4j:jcl-over-slf4j:1.7.5')
    compile('ch.qos.logback:logback-classic:1.0.13')
    compile('org.apache.httpcomponents:httpclient:4.2.3')
    compile('org.codehaus.jackson:jackson-core-asl:1.9.13')
    compile('org.codehaus.jackson:jackson-mapper-asl:1.9.13')
    compile('org.springframework:spring-core:4.1.1.RELEASE'){
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}